# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/crypto.gni")

# GYP version: components/gcm_driver.gypi:gcm_driver_crypto
source_set("crypto") {
  sources = [
    "encryption_header_parsers.cc",
    "encryption_header_parsers.h",
    "gcm_encryption_provider.cc",
    "gcm_encryption_provider.h",
    "gcm_key_store.cc",
    "gcm_key_store.h",
    "gcm_message_cryptographer.cc",
    "gcm_message_cryptographer.h",
    "gcm_message_cryptographer_nss.cc",
    "gcm_message_cryptographer_openssl.cc",
    "p256_key_util.cc",
    "p256_key_util.h",
    "p256_key_util_nss.cc",
    "p256_key_util_openssl.cc",
  ]

  deps = [
    "//base",
    "//components/gcm_driver/common",
    "//components/gcm_driver/crypto/proto",
    "//components/leveldb_proto",
    "//crypto",
    "//crypto:platform",
    "//net",
    "//third_party/protobuf:protobuf_lite",
  ]

  if (use_openssl) {
    sources -= [
      "gcm_message_cryptographer_nss.cc",
      "p256_key_util_nss.cc",
    ]
  } else {
    sources -= [
      "gcm_message_cryptographer_openssl.cc",
      "p256_key_util_openssl.cc",
    ]
  }
}

# GYP version: components/gcm_driver.gypi:gcm_driver_crypto_test_support
source_set("test_support") {
  testonly = true
  sources = [
    "gcm_crypto_test_helpers.cc",
    "gcm_crypto_test_helpers.h",
  ]

  deps = [
    ":crypto",
    "//base",
    "//components/gcm_driver/common",
    "//crypto",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "encryption_header_parsers_unittest.cc",
    "gcm_encryption_provider_unittest.cc",
    "gcm_key_store_unittest.cc",
    "gcm_message_cryptographer_unittest.cc",
    "p256_key_util_unittest.cc",
  ]

  deps = [
    ":crypto",
    "//base",
    "//components/gcm_driver/common",
    "//crypto",
    "//crypto:platform",
    "//testing/gtest",
    "//third_party/protobuf:protobuf_lite",
  ]
}
