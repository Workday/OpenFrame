# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

source_set("language_detection") {
  sources = [
    "language_detection_util.cc",
    "language_detection_util.h",
  ]

  deps = [
    "//base",
    "//components/translate/core/common",
    "//url",
  ]

  if (cld_version == 1) {
    deps += [ "//third_party/cld" ]
  } else if (cld_version == 2) {
    deps += [ "//third_party/cld_2:cld2_platform_impl" ]
  } else {
    assert(false, "Need a CLD version")
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "language_detection_util_unittest.cc",
  ]
  deps = [
    ":language_detection",
    "//base",
    "//components/translate/core/common",
    "//testing/gtest",
    "//url",
  ]
}
