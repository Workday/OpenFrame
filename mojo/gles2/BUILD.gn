# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("mojo_use_gles2") {
  defines = [ "MOJO_USE_GLES2_IMPL" ]
}

config("gles2_use_mojo") {
  defines = [ "GLES2_USE_MOJO" ]
}

source_set("headers") {
  sources = [
    "command_buffer_client_impl.h",
    "gles2_context.h",
  ]

  public_deps = [
    "//base",
    "//components/mus/public/interfaces",
    "//gpu/command_buffer/client",
    "//gpu/command_buffer/client:gles2_implementation",
    "//gpu/command_buffer/common",
    "//mojo/public/c/gles2",
    "//mojo/public/cpp/bindings",
  ]
}

component("gles2") {
  output_name = "mojo_gles2_impl"
  sources = [
    "command_buffer_client_impl.cc",
    "command_buffer_client_impl.h",
    "gles2_context.cc",
    "gles2_context.h",
    "gles2_impl.cc",
  ]

  defines = [
    "GL_GLEXT_PROTOTYPES",
    "MOJO_GLES2_IMPLEMENTATION",
  ]

  configs += [
    ":gles2_use_mojo",
    ":mojo_use_gles2",
  ]
  public_configs = [ ":gles2_use_mojo" ]
  all_dependent_configs = [ ":mojo_use_gles2" ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//components/mus/gles2:lib",
    "//components/mus/public/interfaces",
    "//gpu/command_buffer/client",
    "//gpu/command_buffer/client:gles2_cmd_helper",
    "//gpu/command_buffer/client:gles2_implementation",
    "//gpu/command_buffer/client:gles2_interface",
    "//gpu/command_buffer/common",
    "//mojo/environment:chromium",
    "//mojo/platform_handle:for_component",
    "//mojo/public/c/gles2",
    "//mojo/public/c/system:for_component",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
  ]

  if (!is_component_build) {
    deps += [ "//gpu/command_buffer/service:service_sources" ]
  }

  include_dirs = [ ".." ]
}
